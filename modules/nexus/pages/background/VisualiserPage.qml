import QtQuick
import QtQuick.Layouts
import Caelestia.Config
import qs.components.controls
import qs.modules.nexus.common

PageBase {
    id: root
    title: qsTr("Background visualiser")
    isSubPage: true

    ColumnLayout {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.top: parent.top
        width: root.cappedWidth
        spacing: Tokens.spacing.extraSmall / 2

        SectionHeader {
            first: true
            text: qsTr("General")
        }

        ToggleRow {
            first: true
            Layout.fillWidth: true
            text: qsTr("Enable background visualiser")
            configNode: root.targetConfig.background.visualiser
            propertyName: "enabled"
            checked: root.targetConfig.background.visualiser.enabled
            onToggled: {
                root.targetConfig.background.visualiser.enabled = checked;
                root.targetConfig.save();
            }
        }

        ToggleRow {
            Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
            Layout.fillWidth: true
            text: qsTr("Auto-hide visualiser")
            subtext: qsTr("Hide visualiser when a window is open")
            configNode: root.targetConfig.background.visualiser
            propertyName: "autoHide"
            checked: root.targetConfig.background.visualiser.autoHide
            onToggled: {
                root.targetConfig.background.visualiser.autoHide = checked;
                root.targetConfig.save();
            }
            enabled: root.targetConfig.background.visualiser.enabled
        }

        ToggleRow {
            Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
            Layout.fillWidth: true
            text: qsTr("Blur background")
            subtext: qsTr("Blur the wallpaper behind the visualiser")
            configNode: root.targetConfig.background.visualiser
            propertyName: "blur"
            checked: root.targetConfig.background.visualiser.blur
            onToggled: {
                root.targetConfig.background.visualiser.blur = checked;
                root.targetConfig.save();
            }
            enabled: root.targetConfig.background.visualiser.enabled
        }

        SelectRow {
            Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
            last: true
            Layout.fillWidth: true
            label: qsTr("Renderer")
            configNode: root.targetConfig.background.visualiser
            propertyName: "renderer"
            menuItems: [
                MenuItem { text: qsTr("GPU (Shader)") },
                MenuItem { text: qsTr("CPU (QPainter)") }
            ]
            active: root.targetConfig.background.visualiser.renderer === "cpu" ? menuItems[1] : menuItems[0]
            onSelected: item => {
                root.targetConfig.background.visualiser.renderer = item === menuItems[1] ? "cpu" : "gpu";
                root.targetConfig.save();
            }
            enabled: root.targetConfig.background.visualiser.enabled
        }

        SectionHeader {
            text: qsTr("App filter")
        }

        SelectRow {
            first: true
            Layout.fillWidth: true
            label: qsTr("Filter mode")
            subtext: qsTr("Only show visualiser for specific apps")
            configNode: root.targetConfig.background.visualiser
            propertyName: "appFilter"
            menuItems: [
                MenuItem { text: qsTr("Disabled") },
                MenuItem { text: qsTr("Whitelist") },
                MenuItem { text: qsTr("Blacklist") }
            ]
            active: {
                switch (root.targetConfig.background.visualiser.appFilter) {
                case VisualiserAppFilter.Whitelist: return menuItems[1];
                case VisualiserAppFilter.Blacklist: return menuItems[2];
                default: return menuItems[0];
                }
            }
            onSelected: item => {
                if (item === menuItems[1])
                    root.targetConfig.background.visualiser.appFilter = VisualiserAppFilter.Whitelist;
                else if (item === menuItems[2])
                    root.targetConfig.background.visualiser.appFilter = VisualiserAppFilter.Blacklist;
                else
                    root.targetConfig.background.visualiser.appFilter = VisualiserAppFilter.Disabled;
                root.targetConfig.save();
            }
            enabled: root.targetConfig.background.visualiser.enabled
        }

        TagRow {
            last: true
            Layout.fillWidth: true
            label: qsTr("Filtered apps")
            subtext: qsTr("App names to include or exclude (case-insensitive)")
            configNode: root.targetConfig.background.visualiser
            propertyName: "filteredApps"
            values: root.targetConfig.background.visualiser.filteredApps
            onValueAdded: value => {
                const apps = root.targetConfig.background.visualiser.filteredApps;
                apps.push(value);
                root.targetConfig.background.visualiser.filteredApps = apps;
                root.targetConfig.save();
            }
            onValueRemoved: index => {
                const apps = root.targetConfig.background.visualiser.filteredApps;
                apps.splice(index, 1);
                root.targetConfig.background.visualiser.filteredApps = apps;
                root.targetConfig.save();
            }
            enabled: root.targetConfig.background.visualiser.enabled && root.targetConfig.background.visualiser.appFilter !== VisualiserAppFilter.Disabled
        }

        SectionHeader {
            text: qsTr("Appearance")
        }

        SliderRow {
            first: true
            Layout.fillWidth: true
            label: qsTr("Rounding")
            configNode: root.targetConfig.background.visualiser
            propertyName: "rounding"
            value: root.targetConfig.background.visualiser.rounding / 10.0
            valueLabel: (value * 10.0).toFixed(1)
            onMoved: v => {
                root.targetConfig.background.visualiser.rounding = v * 10.0;
                root.targetConfig.save();
            }
        }

        SliderRow {
            Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
            Layout.fillWidth: true
            label: qsTr("Spacing")
            configNode: root.targetConfig.background.visualiser
            propertyName: "spacing"
            value: root.targetConfig.background.visualiser.spacing / 10.0
            valueLabel: (value * 10.0).toFixed(1)
            onMoved: v => {
                root.targetConfig.background.visualiser.spacing = v * 10.0;
                root.targetConfig.save();
            }
        }

        StepperRow {
            Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
            last: true
            Layout.fillWidth: true
            label: qsTr("Visualiser bars")
            subtext: qsTr("Number of bars in the audio visualisers")
            configNode: root.targetConfig.services
            propertyName: "visualiserBars"
            value: root.targetConfig.services.visualiserBars
            from: 10
            to: 120
            stepSize: 2
            onMoved: v => {
                root.targetConfig.services.visualiserBars = v;
                root.targetConfig.save();
            }
        }
    }
}
